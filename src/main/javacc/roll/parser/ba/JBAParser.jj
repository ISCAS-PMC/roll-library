/* Copyright (c) 2016, 2017                                               */
/*       Institute of Software, Chinese Academy of Sciences               */
/* This file is part of ROLL, a Regular Omega Language Learning library.  */
/* ROLL is free software: you can redistribute it and/or modify           */
/* it under the terms of the GNU General Public License as published by   */
/* the Free Software Foundation, either version 3 of the License, or      */
/* (at your option) any later version.                                    */

/* This program is distributed in the hope that it will be useful,        */
/* but WITHOUT ANY WARRANTY; without even the implied warranty of         */
/* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          */
/* GNU General Public License for more details.                           */

/* You should have received a copy of the GNU General Public License      */
/* along with this program.  If not, see <http://www.gnu.org/licenses/>.  */

options
{
  JDK_VERSION = "1.8";

  LOOKAHEAD= 2;
// FORCE_LA_CHECK = true;
  STATIC = false;
}

PARSER_BEGIN(JBAParser)
package roll.parser.ba;

/**
 * @author Yong Li (liyong@ios.ac.cn)
 * */

public class JBAParser
{
  private BAParser parser ;
  
  public void parse(BAParser parser)
  {
    this.parser = parser;
    try
    {
      this.parser.parseBegin();
      parseAutomaton();
      this.parser.parseEnd();
    }
    catch (ParseException e)
    {
      e.printStackTrace();
    }
  }
}

PARSER_END(JBAParser)

SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
  // skip comments
| < "%" (~[ "\r", "\n" ])* >
| < "//" (~[ "\r", "\n" ])* >
}

TOKEN : /* OPERATORS */
{
  < COMMA : "," >
| < ARROW : "->" >
| < STRING : (~[ ",", ">", "-", "\n", "\r" ])+ > // allow space and tab character in String
  //| < NEWLINE : >
}

private void parseAutomaton() :
{
  Token stateOrLetterStr, labelStr, sourceStr, targetStr;
}
{
  stateOrLetterStr = < STRING > // state or label string
  (
    < COMMA > // no initial state so start with transitions
    sourceStr = < STRING >
    {
      labelStr = stateOrLetterStr;
      parser.setInitial(sourceStr.toString());
    }
    < ARROW > 
    targetStr = < STRING >
  |
    // there is an initial state
    {
      parser.setInitial(stateOrLetterStr.toString());
    }
    labelStr = < STRING > 
    < COMMA > 
    sourceStr = < STRING >
    < ARROW > 
    targetStr = < STRING >
  )
  {
    parser.addTransition(sourceStr.toString(), targetStr.toString(), labelStr.toString());
  }
  parseTransitions() 
  parseFinalStates()
}

private void parseTransitions() :
{
  Token labelStr, sourceStr, targetStr;
}
{
  
  (
    LOOKAHEAD(< STRING> < COMMA>)
    labelStr = < STRING > 
    < COMMA > 
    sourceStr = < STRING >
    < ARROW > 
    targetStr = < STRING >
  {
    parser.addTransition(sourceStr.toString(), targetStr.toString(), labelStr.toString());
  }
  )*
}

// optional 
private void parseFinalStates() :
{
  Token stateIdStr;
}
{
  (
    stateIdStr = < STRING >
    {
      parser.setAccepting(stateIdStr.toString());
    }
  )*
}
